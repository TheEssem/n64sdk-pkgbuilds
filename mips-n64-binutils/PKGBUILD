# Maintainer: Essem <smswessem@gmail.com>

_target=mips-n64
pkgname=${_target}-binutils
pkgver=2.39
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files (${_target})"
url="https://www.gnu.org/software/binutils/"
arch=('x86_64')
license=('GPL')
groups=(n64sdk)
depends=('libelf')
source=("https://ftpmirror.gnu.org/gnu/binutils/binutils-${pkgver}.tar.xz" "gas-vr4300.patch")
sha256sums=('645c25f563b8adc0a81dbd6a41cffbf4d37083a382e02d5d3df4f65c09516d00' '562f0e9e4dd3bcc229cad536438dbc75f324b69302103eaa53a81c3c1b32dab0')

prepare() {
  cd binutils-${pkgver}
  patch -p1 < "${srcdir}/gas-vr4300.patch"
}

build() {
  cd binutils-${pkgver}

  INSTALL_PATH=/opt/crashsdk

  CFLAGS="-O2" CXXFLAGS="-O2" ./configure \
    --disable-debug \
    --enable-checking=release \
    --prefix="$INSTALL_PATH" \
    --target=mips64-elf \
    --with-cpu=mips64vr4300 \
    --program-prefix=mips-n64- \
    --disable-werror

  make
}

check() {
  cd binutils-${pkgver}

  # unset LDFLAGS as testsuite makes assumptions about which ones are active
  # do not abort on errors - manually check log files
  make LDFLAGS="" -k check || true
}

package() {
  cd binutils-${pkgver}
  make DESTDIR="$pkgdir" install-strip

  rm -r "$pkgdir"/opt/crashsdk/share/info
}
