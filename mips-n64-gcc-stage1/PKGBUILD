# Maintainer: Essem <smswessem@gmail.com>

_target=mips-n64
pkgname=${_target}-gcc-stage1
pkgver=12.2.0
pkgrel=1
pkgdesc="The GNU Compiler Collection. Stage 1 for toolchain building (${_target})"
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL' 'custom')
groups=(n64sdk)
url="https://www.gnu.org/software/gcc/"
depends=('libmpc' 'zstd' "${_target}-binutils")
makedepends=('gmp' 'mpfr')
options=('!emptydirs' '!distcc' '!strip')
provides=("${_target}-gcc")
source=("https://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-${pkgver}.tar.xz" "gcc-vr4300.patch")
sha256sums=('e549cf9cf3594a00e27b6589d4322d70e0720cdd213f39beb4181e06926230ff' '02bec03aaabd4970cb209ac717c4faec5174e4a982a8145db05ed857a1302bb6')

prepare() {
  cd gcc-${pkgver}

  patch -p1 < "${srcdir}/gcc-vr4300.patch"
}

build() {
  mkdir -p "${srcdir}"/build-gcc
  cd build-gcc

  CFLAGS=${CFLAGS/-Werror=format-security/}
  CXXFLAGS=${CXXFLAGS/-Werror=format-security/}
  INSTALL_PATH=/opt/crashsdk

  "${srcdir}"/gcc-${pkgver}/configure \
    --prefix="$INSTALL_PATH" \
    --target=mips64-elf \
    --program-prefix=mips-n64- \
    --with-arch=vr4300 \
    --with-tune=vr4300 \
    --enable-languages=c \
    --without-headers \
    --with-newlib \
    --disable-libssp \
    --disable-multilib \
    --disable-shared \
    --with-gcc \
    --disable-threads \
    --disable-win32-registry \
    --disable-nls \
    --disable-werror \
    --with-system-zlib

  make all-gcc
  make all-target-libgcc CFLAGS_FOR_TARGET="-mabi=32 -ffreestanding -mfix4300 -G 0 -fno-stack-protector -mno-check-zero-division -fwrapv -Os"
}

package() {
  cd build-gcc
  make DESTDIR="${pkgdir}" install-gcc
  make DESTDIR="${pkgdir}" install-target-libgcc

  rm -r "$pkgdir"/opt/crashsdk/share/info
}
