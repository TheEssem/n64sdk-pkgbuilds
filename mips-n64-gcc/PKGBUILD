# Maintainer: Essem <smswessem@gmail.com>

_target=mips-n64
pkgname=${_target}-gcc
pkgver=12.2.0
pkgrel=1
pkgdesc="The GNU Compiler Collection (${_target})"
arch=('x86_64')
groups=(n64sdk)
license=('GPL' 'LGPL' 'FDL' 'custom')
url="https://www.gnu.org/software/gcc/"
depends=('libmpc' 'zstd' "${_target}-binutils")
makedepends=('gmp' 'mpfr' "${_target}-newlib")
optdepends=("${_target}-newlib")
options=('!emptydirs' '!distcc' '!strip')
conflicts=("${_target}-gcc-stage1")
provides=("${_target}-gcc-stage1")
replaces=("${_target}-gcc-stage1")
source=("https://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-${pkgver}.tar.xz" "gcc-vr4300.patch")
sha256sums=('e549cf9cf3594a00e27b6589d4322d70e0720cdd213f39beb4181e06926230ff' '02bec03aaabd4970cb209ac717c4faec5174e4a982a8145db05ed857a1302bb6')

prepare() {
  cd gcc-${pkgver}

  patch -p1 < "${srcdir}/gcc-vr4300.patch"
}

build() {
  mkdir -p "${srcdir}"/build-gcc
  cd build-gcc

  CFLAGS=${CFLAGS/-Werror=format-security/}
  CXXFLAGS=${CXXFLAGS/-Werror=format-security/}
  INSTALL_PATH=/opt/crashsdk

  CFLAGS="-O2" CXXFLAGS="-O2" "${srcdir}"/gcc-${pkgver}/configure \
    --prefix="$INSTALL_PATH" \
    --with-gnu-as=${INSTALL_PATH}/bin/mips-n64-as \
    --with-gnu-ld=${INSTALL_PATH}/bin/mips-n64-ld \
    --enable-checking=release \
    --program-prefix=mips-n64- \
    --target=mips64-elf \
    --with-arch=vr4300 \
    --with-tune=vr4300 \
    --enable-languages=c,c++ \
    --with-newlib \
    --disable-libssp \
    --disable-multilib \
    --disable-shared \
    --with-gcc \
    --disable-threads \
    --disable-win32-registry \
    --disable-nls \
    --with-system-zlib

  make CFLAGS_FOR_TARGET="-mabi=32 -ffreestanding -mfix4300 -G 0 -fno-PIC -fwrapv -fno-stack-protector -mno-check-zero-division -Os" CXXFLAGS_FOR_TARGET="-mabi=32 -ffreestanding -mfix4300 -G 0 -fno-stack-protector -mno-check-zero-division -fno-PIC -fno-rtti -Os -fno-exceptions"
}

package() {
  cd build-gcc
  make DESTDIR="${pkgdir}" install-strip

  rm -r "$pkgdir"/opt/crashsdk/share/info
}
