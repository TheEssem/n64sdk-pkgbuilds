# Maintainer: Essem <smswessem@gmail.com>

_target=mips-n64
pkgname=${_target}-newlib
pkgver=4.3.0.20230120
pkgrel=1
pkgdesc="A C standard library implementation intended for use on embedded systems (${_target})"
url='https://www.sourceware.org/newlib/'
arch=('x86_64')
groups=(n64sdk)
license=('BSD')
makedepends=(${_target}-gcc)
source=(https://sourceware.org/pub/newlib/newlib-${pkgver}.tar.gz)
sha256sums=('83a62a99af59e38eb9b0c58ed092ee24d700fff43a22c03e433955113ef35150')

build() {
  cd newlib-${pkgver}

  INSTALL_PATH=/opt/crashsdk
  
  RANLIB_FOR_TARGET=${INSTALL_PATH}/bin/mips-n64-ranlib CC_FOR_TARGET=${INSTALL_PATH}/bin/mips-n64-gcc CXX_FOR_TARGET=${INSTALL_PATH}/bin/mips-n64-g++ AR_FOR_TARGET=${INSTALL_PATH}/bin/mips-n64-ar CFLAGS_FOR_TARGET="-mabi=32 -ffreestanding -mfix4300 -G 0 -fno-stack-protector -mno-check-zero-division -fno-PIC -fwrapv -Os" CXXFLAGS_FOR_TARGET="-mabi=32 -ffreestanding -mfix4300 -G 0 -fno-stack-protector -mno-check-zero-division -fno-PIC -fwrapv -Os" ./configure \
    --target=mips64-elf \
    --prefix="$INSTALL_PATH" \
    --with-cpu=mips64vr4300 \
    --disable-threads \
    --disable-libssp \
    --disable-werror
  make
}

package() {
  cd newlib-${pkgver}
  make DESTDIR="$pkgdir" install
}
